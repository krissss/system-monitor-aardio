//RUNAS/#/cpuTemp
import win.ui;
import win.ui.menu;
/*DSG{{*/
winform = win.form(text="System Monitor Setting";right=510;bottom=413;bgcolor=16777215;max=false)
winform.add()
/*}}*/

import config;
import tools;

if (config.__single && !tools.singleInstance(winform)) {
	return; 
}

// 创建菜单
var menuTable = {
	{
		"信息";
		function(id){
			winform.show(true)
		}
	};
	{
		"设置";
		function(id){
			winform.show(true)
		}
	};
	{ "退出"; function() { winform.onClose=null; winform.close() } };
}
// 创建托盘菜单
winform.popmenu = tools.createPopMenu(winform, menuTable, 'popmenu')
// 创建主菜单
winform.mainmenu = tools.createMainMenu(winform, menuTable)
// 创建托盘
winform.tray = tools.createTray(winform, "\res\icons\mario-ico\0.ico", "Running")

// 托盘更新的定时器
var trayIconIndex = 0
var trayUpdateTimer = tools.createTimeInterval(winform, function(){
	winform.tray.icon = "\res\icons\mario-ico\"+trayIconIndex+".ico"
	trayIconIndex += 1
	if (trayIconIndex > 2) {
		trayIconIndex = 0
	}
}, 100, true)
// 系统信息监控的定时器
import openHardwareMonitor;
var systemMonitorTimer = tools.createTimeInterval(winform, function(){
	var cpuPercent = openHardwareMonitor.getAndUpdateCpuUsage()
	// 修改频率
	winform.tray.tip = 'CPU: ' + cpuPercent + '%'
	trayUpdateTimer.setInterval(100 - cpuPercent)
}, 1000, true)

// 注册窗口事件监听
tools.winformEventHandle(winform)
// 窗口启动
winform.show(true);
return win.loopMessage();